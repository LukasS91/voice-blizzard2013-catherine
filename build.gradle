task copyWav(type: Copy) {
    from(project(':blizzard2013-data').projectDir) {
        include 'wav/*.wav'
        rename {
            def m = (it =~ /(.+-)(\d+)(\.wav)/)
            assert m.matches(): it
            sprintf("%s%03d%s", m[0][1], m[0][2] as int, m[0][3]).replaceAll('-', '_')
        }
    }
    into buildDir
}

task copyPitch(type: Copy) {
    from(project(':blizzard2013-data').projectDir) {
        include 'pm/*.Pitch'
    }
    into buildDir
}

task copyLabels(type: Copy) {
    from(project(':blizzard2013-data').projectDir) {
        include 'lab/*.lab'
    }
    into buildDir
}

task extractTextFiles {
    def txtFile = file("src/raw/resources/all.txt")
    def destDir = file("$buildDir/text")
    outputs.dir destDir
    inputs.files txtFile
    doFirst {
        destDir.mkdirs()
    }
    doLast {
        txtFile.eachLine {
            def (basename, content) = it.split(":", 2)
            file("$destDir/${basename}.txt").text = content.trim()
        }
    }
}
